## Guardian Druid
# Based on WoWSims

## Precombat
actions.precombat+=/mark_of_the_wild,if=!buff.stat_buff.up
actions.precombat+=/thorns,if=!up
actions.precombat+=/bear_form,if=!up
actions.precombat+=/potion


## Action-List Selection
actions+=/use_items
actions+=/potion
actions+=/hyperspeed_acceleration,use_off_gcd=1
actions+=/run_action_list,name=bear_tank_aoe,if=buff.bear_form.up&active_enemies>2
actions+=/run_action_list,name=bear_tank,if=buff.bear_form.up
actions+=/run_action_list,name=cat_aoe,if=buff.cat_form.up&active_enemies>2
actions+=/run_action_list,name=cat,if=buff.cat_form.up
actions+=/bear_form,if=!up

## Single Target
actions.bear_tank+=/frenzied_regeneration,if=health.pct<30
actions.bear_tank+=/survival_instincts,if=health.pct<40
actions.bear_tank+=/feral_charge_bear,if=target.outside7
actions.bear_tank+=/maul,if=rage.current>=55
actions.bear_tank+=/pulverize,if=dot.lacerate.ticking&dot.lacerate.stack=3&dot.lacerate.remains<4
actions.bear_tank+=/lacerate,cycle_targets=1,max_cycle_targets=2,if=dot.lacerate.ticking&dot.lacerate.remains<4
actions.bear_tank+=/faerie_fire_feral,cycle_targets=1,max_cycle_targets=2,if=settings.maintain_ff&(debuff.major_armor_reduction.down|(debuff.faerie_fire.up&debuff.faerie_fire.remains<6))
actions.bear_tank+=/demoralizing_roar,if=settings.maintain_roar&(debuff.ap_reduction.down|(debuff.demoralizing_roar.up&debuff.demoralizing_roar.remains<4))
actions.bear_tank+=/berserk
actions.bear_tank+=/enrage,if=rage.current<=80
actions.bear_tank+=/synapse_springs
actions.bear_tank+=/thrash
actions.bear_tank+=/mangle_bear
actions.bear_tank+=/lacerate,cycle_targets=1,max_cycle_targets=2,if=!dot.lacerate.ticking&!buff.berserk.up
actions.bear_tank+=/pulverize,cycle_targets=1,max_cycle_targets=2,if=dot.lacerate.ticking&dot.lacerate.stack=3&(!buff.pulverize.up|buff.pulverize.remains<4)
actions.bear_tank+=/lacerate,cycle_targets=1,max_cycle_targets=2,if=dot.lacerate.stack<3
actions.bear_tank+=/faerie_fire_feral
actions.bear_tank+=/maul

## Multi Target
actions.bear_tank_aoe+=/frenzied_regeneration,if=health.pct<30
actions.bear_tank_aoe+=/survival_instincts,if=health.pct<40
actions.bear_tank_aoe+=/feral_charge_bear,if=target.outside7
actions.bear_tank_aoe+=/maul,if=rage.current>=55
actions.bear_tank_aoe+=/pulverize,if=dot.lacerate.ticking&dot.lacerate.stack=3&dot.lacerate.remains<4
actions.bear_tank_aoe+=/faerie_fire_feral,if=settings.maintain_ff&(debuff.major_armor_reduction.down|(debuff.faerie_fire.up&debuff.faerie_fire.remains<6))
actions.bear_tank_aoe+=/demoralizing_roar,if=settings.maintain_roar&(debuff.ap_reduction.down|(debuff.demoralizing_roar.up&debuff.demoralizing_roar.remains<4))
actions.bear_tank_aoe+=/thrash
actions.bear_tank_aoe+=/swipe_bear
actions.bear_tank_aoe+=/berserk
actions.bear_tank_aoe+=/enrage,if=rage.current<=80
actions.bear_tank_aoe+=/synapse_springs
actions.bear_tank_aoe+=/lacerate,if=!dot.lacerate.ticking&!buff.berserk.up
actions.bear_tank_aoe+=/mangle_bear
actions.bear_tank_aoe+=/faerie_fire_feral
actions.bear_tank_aoe+=/pulverize,if=dot.lacerate.ticking&dot.lacerate.stack=3&(!buff.pulverize.up|buff.pulverize.remains<4)
actions.bear_tank_aoe+=/lacerate,if=dot.lacerate.stack<3
actions.bear_tank_aoe+=/maul

## Cat rotation if cat_form active
actions.cat+=/synapse_springs,if=try_tigers_fury|try_berserk
actions.cat+=/tigers_fury,use_off_gcd=1,if=try_tigers_fury
actions.cat+=/berserk,use_off_gcd=1,if=try_berserk
actions.cat+=/cancel_buff,buff_name=primal_madness,if=cancel_primal_madness&buff.primal_madness.up&energy.current<20
actions.cat+=/faerie_fire_feral,if=ff_now&target.outside2
actions.cat+=/feral_charge_cat,if=target.outside7
actions.cat+=/mangle_cat,if=feral_t11_refresh_now
actions.cat+=/rip,if=rip_now
actions.cat+=/savage_roar,if=roar_now&!rip_now
actions.cat+=/ferocious_bite,if=bite_now&(energy.current>=action.ferocious_bite.spend|!should_bearweave)&!(rip_now|roar_now)
actions.cat+=/mangle_cat,if=mangle_now
actions.cat+=/rake,if=rake_now
actions.cat+=/mangle_cat,if=feral_t11_build_now
actions.cat+=/bear_form,if=should_bearweave&!(feral_t11_refresh_now|rip_now|roar_now|mangle_now|rake_now|feral_t11_build_now)
actions.cat+=/feral_charge_cat,if=should_leaveweave
actions.cat+=/ravage,if=ravage_now
actions.cat+=/shred,if=(excess_e>=action.shred.spend|buff.clearcasting.up|buff.berserk.up|energy.current>=energy.max-energy.regen*latency)

## Cat AoE: Default Rotation when in cat_form and multi target
# try to maintain t11 during aoe and cycle on unmangled enemy for max value
actions.cat_aoe+=/mangle_cat,cycle_targets=1,max_cycle_targets=5,if=feral_t11_refresh_now&!debuff.mangle.up&target.within2
# try to maintain t11 during aoe even without cycle
actions.cat_aoe+=/mangle_cat,if=feral_t11_refresh_now
actions.cat_aoe+=/synapse_springs,if=try_tigers_fury|try_berserk|buff.berserk.up
actions.cat_aoe+=/tigers_fury,use_off_gcd=1,if=try_tigers_fury
actions.cat_aoe+=/berserk,if=try_berserk
actions.cat_aoe+=/savage_roar,if=!up&ttd>2+latency
actions.cat_aoe+=/swipe_cat,if=active_enemies>6|active_enemies>3&buff.tigers_fury.up
actions.cat_aoe+=/ravage,if=ravage_now
actions.cat_aoe+=/rake,cycle_targets=1,max_cycle_targets=5,if=active_enemies<7&target.within3&((!debuff.rake.up|(debuff.rake.remains<debuff.rake.tick_time))&(ttd>debuff.rake.tick_time))
actions.cat_aoe+=/swipe_cat
# Bearweave if we're out of energy and TF on cd
actions.cat_aoe+=/bear_form,if=should_bearweave